ex =: 3 : 0
i =. 2 {. I. 4 = {. y
'ld l' =. y {."1~ {. i
'rd r' =. y }."1~ >: {: i
n =. {: y
'dv nv' =. i {"1 y
l =. ({. nv) (}:@:] , ([ + {:@:]))^:(0 < #@]) n {.~ {. i
r =. ({: nv) (([ + {.@:]) , }.@:])^:(0 < #@]) n }.~ >: {: i 
(ld , (>./ <: {. dv) , rd) ,: l,0,r
)

sp =: 3 : 0
i =. {. I. 10 <: {: y
'd v' =.  i {"1 y
'ld l' =. i {."1 y
'rd r' =. y }."1~ >: i
d =. ld , (2 $ >: d), rd
n =. l,  ((<. , >.) -: v) ,r
d ,: n
)

mag =: 3 : 0
i =. 2 {. I. (+ _1 |. ]) ,&0 ] 2 =/\ {. y
'ld l' =. y {."1~ {. i
'rd r' =. y }."1~ >: {: i
v =. y {"1~ i
d =. (ld,(>./ 0 , <: {. v), rd)
n =. (l ,(+/ 3 2 * {: v ) , r) 
d ,: n
)

pd =: <:@(-.@e.&'[],' # [: +/\@:(* -.@>&1)@:<: '] ['&i.)
p =: pd ,: ".@-.&'[]'

red =: (]`sp@.([: +./ 10 <: {:))`ex@.([: 4&e. {.)^:_
add =:  [: ({: ,:~ [: >: {.) (,"1)

inp =: <@:p;._2 getin 21 18
p1 =: {: mag^:(1 < [: # {.)^:_ red@:add~&:>/ |. inp
p2 =: >./ , ([: {: mag^:(1 < [: # {.)^:_)@:red@:add&:>"0 0/~ inp
NB. >./ , ([: {: mag^:(1 < [: # {.)^:_)@:red@:(add~)&:>"+/~ inp
p1;p2
